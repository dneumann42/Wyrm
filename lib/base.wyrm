transparent-fun do-times {n blk} do
  if {> $n 0} do
    var result [eval $blk]
    if {break? $result} do
      0
    else
      if {return? $result} do
        propagate $result
      else
        do-times [- $n 1] $blk
      end
    end
  end
end

transparent-fun while {exp blk} do
  if {eval $exp} do
    var result [eval $blk]
    if {break? $result} do
      0
    else
      if {return? $result} do
        propagate $result
      else
        recr {while $exp $blk}
      end
    end
  end
end

transparent-fun for {name start limit blk} do
  transparent-fun loop {i} do
    var $name $i
    var result [eval $blk]
    if {break? $result} do
      0
    else
      if {return? $result} do
        propagate $result
      else
        if {< $i [eval $limit]} do
          loop [+ $i 1]
        end
      end
    end
  end
  loop $start
end

fun cond {clauses} do
  fun walk {i} do
    if {< $i [+ [len $clauses] 1]} do
      var clause [ref $clauses $i]
      if {eval [ref $clause 1]} do
        eval [ref $clause 2]
      else
        walk [+ $i 1]
      end
    end
  end
  walk 1
end

export for while do-times read-line cond
